-- @path QWidget=/QWidget/model/qWidget.ecore
-- @path QtQuick=/QtQuick/model/qtQuick.ecore

module QWidget2QtQuick;
create OUT : QtQuick from IN : QWidget;

rule Window {
	from
		s : QWidget!QMainWindow
	to 
		t : QtQuick!ApplicationWindow (
			title <- s.windowTitle,
			width <- s.width,
			height <- s.height,
			content <- s.content
		)
}
helper context QWidget!QObject def : isRootElement() : Boolean =
	self.refImmediateComposite().oclIsTypeOf(QWidget!QMainWindow);

rule Object {
	from
		s : QWidget!QObject
	to 
		t : QtQuick!Item (
			id <- s.name,
			
			anchors_fill <- if (s.isRootElement())
			then 'parent'
			else OclUndefined
			endif,
			
			anchors_margins <- if (s.isRootElement())
			then 11
			else OclUndefined
			endif
		)		
}


rule Layout extends Object {
	from
		s : QWidget!QLayout
	to 
		t : QtQuick!Item (
			contains <- thisModule.stretchRecursive(s.contains, s.contains.size())->append(thisModule.newItem()),
			--contains <- s.contains,
			Layout_fillHeight <- true,
			Layout_fillWidth <- true,
			spacing <- 0
		)
}

-- add stretching Item after every QObject
helper def : stretchRecursive(seq: Sequence(OclAny), index: Integer) : Sequence(OclAny) =
	if index <= 0
	then seq.debug()
	else self.stretchRecursive(seq->insertAt(index, thisModule.newItem()), index-1)
	endif;

lazy rule newItem {
	from
    	e: ecore!EObject
	to
		i: QtQuick!Item (
			Layout_fillHeight <- true,
			Layout_fillWidth <- true,
			anchors_margins <- 0
		)
}
	

rule verticalLayout extends Layout {
	from
		s : QWidget!QVBoxLayout
	to 
		t : QtQuick!ColumnLayout
}

rule horizontalLayout extends Layout {
	from
		s : QWidget!QHBoxLayout
	to 
		t : QtQuick!RowLayout
}

rule Button extends Object {
	from
		s : QWidget!QPushButton
	to 
		t : QtQuick!Button (
			text <- s.text,
			Layout_fillWidth <- true
		)
}

rule RadioButton extends Object {
	from
		s : QWidget!QRadioButton
	to 
		t : QtQuick!RadioButton (
			text <- s.text,
			checked <- s.checked,
			Layout_fillWidth <- true
		)
}

rule CheckBox extends Object {
	from
		s : QWidget!QCheckBox
	to 
		t : QtQuick!CheckBox (
			text <- s.text,
			checked <- s.checked,
			Layout_fillWidth <- true
		)
}

rule Font {
	from
		s : QWidget!Font
	to
		t : QtQuick!Font (
			family <- s.family,
			pointsize <- s.pointsize,
			weight <- s.weight,
			bold <- s.bold,
			italic <- s.italic,
			underline <- s.underline,
			strikeout <- s.strikeout
		)
}

rule Label extends Object {
	from
		s : QWidget!QLabel
	to 
		t : QtQuick!Label (
			text <- s.text,
			verticalAlignment <- s.verticalAlignment,
			horizontalAlignment <- s.horizontalAlignment,
			Layout_fillWidth <- true,
			font <- s.font
		)
}

rule TextArea extends Object {
	from
		s : QWidget!QTextEdit
	to 
		t : QtQuick!TextArea (
			Layout_fillHeight <- true,
			Layout_fillWidth <- true
		)
}
